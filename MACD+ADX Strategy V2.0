//@version=4
strategy("MACD + ADX Strategy V2.0", overlay=true)

startDate = input(title="Start Date", type=input.integer,
     defval=1, minval=1, maxval=31)
startMonth = input(title="Start Month", type=input.integer,
     defval=1, minval=1, maxval=12)
startYear = input(title="Start Year", type=input.integer,
     defval=2019, minval=1900, maxval=2100)
     
endDate = input(title="End Date", type=input.integer,
     defval=1, minval=1, maxval=31)
endMonth = input(title="End Month", type=input.integer,
     defval=1, minval=1, maxval=12)
endYear = input(title="End Year", type=input.integer,
     defval=2030, minval=1900, maxval=2100)

inDateRange = (time >= timestamp(syminfo.timezone, startYear,
         startMonth, startDate, 0, 0)) and
     (time < timestamp(syminfo.timezone, endYear, endMonth, endDate, 0, 0))

fastLength = input(12)
slowlength = input(26)
MACDLength = input(9)

MACD = ema(close, fastLength) - ema(close, slowlength)
aMACD = ema(MACD, MACDLength)
delta = MACD - aMACD

len = input(14, minval=1, title="DI Length")
lensig = input(14, title="ADX Smoothing", minval=1, maxval=50)

adx_emalen = input(20, title="ADX EMA", minval=1, maxval=50)

up = change(high)
down = -change(low)
trur = rma(tr, len)

plus = fixnan(100 * rma(up > down and up > 0 ? up : 0, len) / trur)
minus = fixnan(100 * rma(down > up and down > 0 ? down : 0, len) / trur)

sum = plus + minus 
adx = 100 * rma(abs(plus - minus) / (sum == 0 ? 1 : sum), lensig)

adx_signalline = ema(adx,adx_emalen)

// Figure out take profit price
TP_perc = input(2, title="Profit Target (%)", type=input.float, minval=0.5, step=0.1)/100
longExitPrice  = strategy.position_avg_price * (1 + TP_perc)
shortExitPrice = strategy.position_avg_price * (1 - TP_perc)

// Plot take profit values for confirmation
//plot(series=(strategy.position_size > 0) ? longExitPrice : na,
//     color=color.green, style=plot.style_circles,
//     linewidth=3, title="Long Take Profit")
//plot(series=(strategy.position_size < 0) ? shortExitPrice : na,
//     color=color.red, style=plot.style_circles,
//     linewidth=3, title="Short Take Profit")

if (inDateRange and delta > 0 and adx_signalline > adx and adx > 20)
    strategy.close(id="MacdSE")
    strategy.entry("MacdLE", strategy.long, comment="LongEntry")
if (strategy.position_size > 0)
    strategy.exit(id="LongTP", limit=longExitPrice)

if (inDateRange and delta < 0 and adx_signalline > adx and adx > 20)
    strategy.close(id="MacdLE")
    strategy.entry("MacdSE", strategy.short, comment="ShortEntry")
if (strategy.position_size < 0)
    strategy.exit(id="ShortTP", limit=shortExitPrice)
